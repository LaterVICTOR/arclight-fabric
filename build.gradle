allprojects {
    apply plugin: 'java'

    group 'io.izzel.arclight'
    version '1.0.4'

    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    ext {
        minecraftVersion = '1.20.1'
        supportedPlatforms = ['forge', 'fabric']
        fabricLoaderVersion = '0.15.11'
        fabricApiVersion = '0.92.2+1.20.1'
        forgeVersion = '47.3.11'
        neoForgeVersion = '20.4.237'
        apiVersion = '1.7.1'
        toolsVersion = '1.3.0'
        mixinVersion = '0.8.5'
        mixinToolsVersion = '1.2.3'
        versionName = 'whisper'
        gitHash = getGitHash()
    }

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    repositories {
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://maven.neoforged.net/releases' }
        maven { url = 'https://files.minecraftforge.net/maven/' }
        maven { url = 'https://maven.izzel.io/releases' }
        maven { url = 'https://jitpack.io/' }
        mavenCentral()
    }

    tasks.register('cleanBuild') {
        doFirst {
            project.file("build/libs").deleteDir()
        }
    }
}

jar.enabled = false

tasks.register('collect', Copy) {
    destinationDir = file('build/libs')
    from project(':bootstrap').tasks.forgeJar.outputs
    from project(':bootstrap').tasks.fabricJar.outputs
}

def gitBranch() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
